finish
/clear

a1=55
a2=25
w1=10
w2=10
h=100
beta=5

delta=h/beta

/prep7
	! не все элементы поддерживают гиперупругость, только новые у которых больше 100 номера plane182
	! осесимметричная постановка
	et,1,plane182 ! 4-х узловой элемент
	keyopt,1,3,1  ! осесиметричная задача
	
	! найти свойства по файлу эксперимента, который определили, будут пересчитваться
	TBFT,EADD,1,UNIA,mat1.txt	! Эксперимент данные
	TBFT,FADD,1,HYPER,MOON,2	! материал модель Муни, добавили
	TBFT,SOLVE,1,HYPER,MOON,2,1 ! Решили	
	TBFT,FSET,1,HYPER,MOON,2	! Задали
	! Материал несжимаемый, объёмные коэфф не могут посчитаться для данного задания
	! d = 0 объемная податливость
	
	! Снизу вверх - ключ точки, линии, область
	K,1,a1-w1/2,0
	K,2,a2-w2/2,h
	K,3,a2+w2/2,h
	K,4,a1+w1/2,0
	
	l,1,2
	l,2,3
	l,3,4
	l,4,1
	
	al,all	! На всем строим область
	
	!	Параметры сетки
	esize,w1/5
	amesh,all
	
	! Закрепим снизу все узлы	
	dl,4,,all
	
	! Все верхние узлы приведем к одному узлу - Preprocessor -> Coupling/Ceqn
	! Реакция в общем узле = суммарной реакции на верхней грани
	! Врезанный шарнир = Concident Nodes (на узлы с совпад. коорд. ограничения накладываем), получается шарнир, либо плавающая заделка (перемещ оставляем, поворот ограничиваем)
	
	! У нас - Cupl DOFs w/Mstr
	! Узел с наим. номером через NSEL - это главный узел, к нему все приводится. Реакция в нем - реакции в заделке, перемещ - перемещ все
	
	nsel,s,loc,y,h	!выбираем все узлы на верхней грани
	CP,,all,all		! объединяем перемещения всех узлов и степ своб
	allsel	!снимаем выделение	
	! Сила, перемещ к главному узлу приложенные, реацкия в нем = реакция суммарная во всей верхней части
	
	! CPLIST - посмотреть набор объединений
	
	d,2,uy,-delta	! прикладываем перемещение ко второму узлу (общему)
	! Y-componrnt disp = X-Axis
	d,2,ux,0	! Запрещаем осевые перемещения
finish

/solution
	! должны указать, что у нас большие перемещения
	nlgeom,on	! включаем большие перемещения
	! Задаем количество шагов, т.к. функция нелинейная, упругий потенц. нелин, решение нелинейно = решаем итерационно с заданием шагов
	nsubst,100	! нач. кол-во шагов
	outres,all,all	! на каждом шаге выдаем все результаты, иначе результат только с последнего шага
	solve	! выполняе решение
	! решение сошлось за 15 шагов
	! PlotCtrls-Style-Symmetry Expansion - 2D Axysymmetric 3/4
	! Stress Intensity
	
	!postproc - Reaction Forces - Y-componrnt, номер узла 2
	!PlotCtrls - Style - Graphs - Modify Axes
finish

/post26
	NUMVAR,200  
	SOLU,191,NCMIT 
	
	! Построение графиков
	RFORCE,2,2,F,Y,FY_2
	NSOL,3,2,U,Y,UY_3,

	FILLDATA,192,,,,0,0 
	FILLDATA,193,,,,1,0 
	FILLDATA,194,,,,-1,0
	FILLDATA,195,,,,1,1

	PROD,199,3,194
	ADD,4,,199,,U
	
	PROD,199,2,194
	ADD,5,,199,,F
	
	/AXLAB,X,Displ, mm
	/AXLAB,Y,Force, N
	
	XVAR,4  
	PLVAR,5
	
	! PlotCtrls - Capture Image 